<?xml version="1.0"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"><html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
<title> Z0mbie 'I.Danilov vs V.Bogdanov (Dr.Web vs AVP): Programmer's Competition' (VX heaven)</title>
<meta http-equiv="content-type" content="text/html; charset=utf-8"/>
<meta name="Author" content="Z0mbie"/>
<meta name="KeyWords" lang="en" content="computer virus, virus, virii,vx, компьютерные вирусы, вирус, вири, Z0mbie,I.Danilov vs V.Bogdanov (Dr.Web vs AVP): Programmer's Competition, emul, test, danilov, bogdanov, checking, code, divisor, ffffh, idiv, dword, correct, word, byte, exception, subroutines"/>
<meta name="Description" content="This  is  a  global  research  of  comparing IQ of two av programmers. Danilov and Bogdanov will be fixed on the october's base and version 4.20, after  that  we will send IDIV command to their input and analyze reaction in the form of the av emulator."/>
<script type="text/javascript">
//<![CDATA[
try{if (!window.CloudFlare) {var CloudFlare=[{verbose:0,p:0,byc:0,owlid:"cf",bag2:1,mirage2:0,oracle:0,paths:{cloudflare:"/cdn-cgi/nexp/dok3v=1613a3a185/"},atok:"047a5bcbf67431883fc9ed25fba33612",petok:"6bdc39e3f36e4aa2d6b220c85d6fb16b57e6ca72-1498756452-1800",zone:"vxheaven.org",rocket:"a",apps:{}}];document.write('<script type="text/javascript" src="//ajax.cloudflare.com/cdn-cgi/nexp/dok3v=85b614c0f6/cloudflare.min.js"><'+'\/script>');}}catch(e){};
//]]>
</script>
<link rel="icon" href="/favicon.ico" type="image/x-icon"/>
<link rel="shortcut icon" href="/favicon.ico" type="image/x-icon"/>
<link rel="stylesheet" type="text/css" href="/style.css"/><link rel="canonical" href="http://vxheaven.org/lib/vzo22.html"/>
<script type="text/rocketscript" data-rocketsrc="https://apis.google.com/js/plusone.js">{"parsetags": "explicit"}</script>
</head>
<body bgcolor="#dbc8a0" text="#302000" link="#225599" vlink="#113366">
<div class="s1">
<div style="float:right;"><a href="/lib/index.php?tbs=1"><img src="/img/max.gif" alt="Maximize"/></a></div> <form id="lf" style="margin: 0; float: right;" method="get" action="/index.php"><input type="hidden" name="action" value="set"/><select name="lang" onchange="javascript:document.getElementById('lf').submit();"><option value="ru">Русский</option><option selected="selected" value="en">English</option><option value="ua">Українська</option><option value="de">Deutsch</option><option value="es">Español</option><option value="fr">Fran&ccedil;ais</option><option value="it">Italiano</option><option value="pl">Polski</option></select></form>
<div style="float: right;"><div id="plusone"></div></div>
<script type="text/rocketscript">gapi.plusone.render("plusone", {"size":"small","count":"true"});</script>
<div style="float: right;" class="addthis_toolbox addthis_default_style">
<script type="text/rocketscript">var addthis_config = { ui_click: true }</script>
<a style="text-decoration: none; font-size: 10pt;" href="/?action=addthis" class="addthis_button_compact">Bookmark</a>
<script type="text/rocketscript" data-rocketsrc="http://s7.addthis.com/js/250/addthis_widget.js#username=herm1t"></script>
</div>
<div style="float: right;">
<script type="text/rocketscript" data-rocketsrc="http://www.google.com/cse/brand?form=cse-search-box&amp;lang=en"></script>
<form action="/search.php" id="cse-search-box">
<input type="hidden" name="cx" value="002577580816726040001:z9_irkorydo"/>
<input type="hidden" name="cof" value="FORID:10"/>
<input type="hidden" name="ie" value="UTF-8"/>
<input type="text" name="q" size="12" value=" "/>
<input type="submit" name="sa" value="Search"/>
</form>
</div><h1><a href="/" style="text-decoration: none; color: #000000;">VX Heaven</a></h1>
<span class="nav"><a href="/lib/">Library</a> <a href="/vl.php">Collection</a> <a href="/src.php">Sources</a> <a href="/vx.php?id=eidx">Engines</a> <a href="/vx.php?id=tidx">Constructors</a> <a href="/vx.php?id=sidx">Simulators</a> <a href="/vx.php?id=uidx">Utilities</a> <a href="/links.php">Links</a> <a href="/donate.php" style="color: #706020" id="donate">Donate</a> <a href="/forum" style="text-decoration: underline;">Forum</a> </span><br clear="all"/>
</div>
<div class="s2"><h1>I.Danilov vs V.Bogdanov (Dr.Web vs AVP): Programmer's Competition</h1><p><a href="/lib/?lang=en&amp;author=Z0mbie"> Z0mbie</a><br/> <em> 2000</em></p><script type="text/rocketscript">var disqus_url = 'http://vxheaven.org/lib/vzo22.html';</script>[<a style="" href="/lib/?lang=EN&amp;index=AR#vzo22">Back to index</a>] [<a href="/lib/vzo22.html#disqus_thread">Comments</a>]<br/> 
<p><em>xlated from russian</em></p>
<p>This is a global research of comparing IQ of two av programmers. Danilov and Bogdanov will be fixed on the october's base and version 4.20, after that we will send IDIV command to their input and analyze reaction in the form of the av emulator.</p>
<p>Now, the problem description.</p>
<p>There are two instructions: DIV and IDIV, performing unsigned and signed division. And there are three forms of each instruction, depending on the size of the divisor: BYTE, WORD or DWORD. Dividend, whose precision is twice larger, is represented in the fixed registers: AX, DX:AX or EDX:EAX. Quotient and remainder are returned in AL:AH, AX:DX or EAX:EDX registers. Because precision of the dividend is twice larger than quoitient's one, there are several situations, when produced result is larger than output register, so result cant be returned, and then CPU generates overflow exception (EXCEPTION_INT_OVERFLOW). Also, there is division-by-zero exception (EXCEPTION_INT_DIVIDE_BY_ZERO), but, it is simple enough to speak about.</p>
<p>It is clear, that program emulating DIV and IDIV commands by means of direct execution, must avoid exception in the own code, by means of input registers checking.</p>
<p>This checking is a subject of the following disasm.</p>
<pre class="source">
=============================================================================
 <strong>Danilov's emulator: drweb32.dll</strong>
=============================================================================

emul_flags      = ebp

--------------------------------------------------------------------web/div8-

emul_div8:      mov     ecx, edx        ; cl = divisor
                and     ecx, 0FFh
                ;;
                mov     eax, emul_eax   ; load emul regs
                and     eax, 0FFFFh     ; AX = dividend
                ;;
                or      ecx, ecx        ; div by 0 ?
                jz      emul_div0
                cmp     ah, cl          ; dividend.Hi >= divisor
                jae     emul_div0
                ;;
                xor     edx, edx        ; prepare to division
                ;;
                push    emul_flags      ; division emulation
                popf
                div     cx
                pushf
                pop     emul_flags
                ;;
                cmp     eax, 0FFh       ; overflow check
                ja      emul_div0
                ;;
                mov     emul_al, al     ; save emul regs
                mov     emul_ah, dl
                ;;
                jmp     emul_complete

-------------------------------------------------------------------web/div16-

emul_div16:     mov     ecx, edx        ; cx &lt;- divisor
                and     ecx, 0FFFFh
                ;;
                mov     eax, emul_eax   ; load emul regs
                and     eax, 0FFFFh     ; DX:AX = dividend
                mov     edx, emul_edx
                and     edx, 0FFFFh
                ;;
                or      ecx, ecx        ; div by 0 ?
                jz      emul_div0
                cmp     edx, ecx        ; dividend.Hi >= divisor
                jae     emul_div0
                ;;
                shl     edx, 10h        ; prepare to division
                mov     dx, ax          ; EDX:EAX &lt;-- dx:ax
                mov     eax, edx
                xor     edx, edx
                ;;
                push    emul_flags      ; division emulation
                popf
                div     ecx
                pushf
                pop     emul_flags
                ;;
                cmp     eax, 0FFFFh     ; overflow check
                ja      emul_div0
                ;;
                mov     emul_ax, ax     ; save emul regs
                mov     emul_dx, dx
                ;;
                jmp     emul_complete

-------------------------------------------------------------------web/div32-

emul_div32:     mov     ecx, edx        ; ecx &lt;- divisor
                ;;
                mov     eax, emul_eax   ; load emul regs
                mov     edx, emul_edx   ; EDX:EAX = dividend
                ;;
                or      edx, edx        ; a bug
                jz      emul_complete   ;
                ;
                or      ecx, ecx        ; div by 0 ?
                jz      emul_div0
                cmp     edx, ecx        ; dividend.Hi >= divisor
                jae     emul_div0
                ;;
                push    emul_flags      ; division emulation
                popf
                div     ecx
                pushf
                pop     emul_flags
                ;;
                mov     emul_eax, eax   ; save emul regs
                mov     emul_edx, edx
                ;;
                jmp     emul_complete

-------------------------------------------------------------------web/idiv8-

emul_idiv8:     movsx   ecx, dl         ; cl = dl = divisor
                ;;
                mov     ax, emul_ax     ; load emul regs
                ;;                      ; AX = dividend
                or      ecx, ecx        ; div by 0 ?
                jz      emul_div0
                ;;
                test    dl, 80h         ; abs(dl)
                jz      @@1
                neg     dl
@@1:            ;;
                test    ah, 80h         ; abs(ah)
                jz      @@2             ; AH = dividend.Hi
                neg     ah
@@2:            ;;
                sub     dl, ah          ; dl &lt;= ah * 2
                jb      emul_div0
                cmp     dl, ah
                jbe     emul_div0
                ;;
                sub     dl, ah          ; dl - ah * 2 != 1
                cmp     dl, 1
                jnz     @@3
                test    al, 80h         ; dividend, high bit set?
                jnz     emul_div0
@@3:            ;;
                mov     ax, emul_ax     ; prepare to division
                cwd                     ; DX:AX &lt;-- AX
                ;;
                push    emul_flags      ; division emulation
                popf
                idiv    cx
                pushf
                pop     emul_flags
                ;;
                cmp     ax, 7Fh         ; overflow check
                ja      emul_div0
                ;;
                mov     emul_al, al     ; save emul regs
                mov     emul_ah, dl
                ;;
                jmp     emul_complete

------------------------------------------------------------------web/idiv16-

emul_idiv16:    movsx   ecx, dx         ; ecx = dx = divisor
                ;;
                mov     ax, emul_dx     ; ax = dividend.Hi
                ;;
                or      ecx, ecx        ; div by 0 ?
                jz      emul_div0
                ;;
                test    dh, 80h         ; abs(dx)
                jz      @@1
                neg     dx
@@1:            ;;
                test    ah, 80h         ; abs(ax)
                jz      @@2
                neg     ax
@@2:            ;;
                sub     dx, ax          ; if (dx &lt;= ax * 2) div0
                jb      emul_div0
                cmp     dx, ax
                jbe     emul_div0
                ;;
                sub     dx, ax          ; if (dx - ax * 2) != 1 { ...
                cmp     dx, 1
                jnz     @@3
                test    emul_ah, 80h    ; dividend.Lo, high bit set?
                jnz     emul_div0       ;
@@3:            ;;
                mov     eax, emul_eax   ; load emul regs
                and     eax, 0FFFFh
                mov     edx, emul_edx
                and     edx, 0FFFFh
                ;;
                or      ecx, ecx        ; div by 0 ?
                jz      emul_div0       ; 2nd checking, for more reliability
                ;;
                shl     edx, 10h        ; prepare to division
                mov     dx, ax          ; EDX:EAX &lt;-- dx:ax
                mov     eax, edx
                cdq
                ;;
                push    emul_flags      ; division emulation
                popf
                idiv    ecx
                pushf
                pop     emul_flags
                ;;
                cmp     eax, 7FFFh      ; overflow check
                ja      emul_div0
                ;;
                mov     emul_ax, ax     ; save emul regs
                mov     emul_dx, dx
                ;;
                jmp     emul_complete

------------------------------------------------------------------web/idiv32-

emul_idiv32:    mov     emul_eax, 0     ; this is cool
                mov     emul_edx, 0
                ;;
                jmp     emul_complete

-----------------------------------------------------------------------------

=============================================================================
 <strong>Bogdanov's emulator: kernel.avc/emul.obj</strong>
=============================================================================

divisor         =       ebx

--------------------------------------------------------------------avp/div8-

emul_div8:      cmp     byte ptr [divisor], 0   ; div by 0 ?
                jz      emul_int0
                ;;
                mov     ax, emul_ax     ; load emul regs
                ;;                      ; AX = dividend
                cmp     ah, [divisor]   ; dividend.Hi >= divisor
                jae     try_emul_int0
                ;;
                push    emul_flags      ; division emulation
                popfw
                div     byte ptr [divisor]
                pushfw
                pop     emul_flags
                ;;
                mov     emul_ax, ax     ; save emul regs
                ;;
                jmp     emul_complete

-------------------------------------------------------------------avp/div16-

emul_div16:     cmp     word ptr [divisor], 0   ; div by 0 ?
                jz      emul_int0
                ;;
                mov     ax, emul_ax     ; load emul regs
                mov     dx, emul_dx     ; DX:AX = dividend
                ;;
                cmp     dx, [divisor]   ; dividend.Hi >= divisor
                jae     try_emul_int0
                ;;
                push    emul_flags      ; division emulation
                popfw
                div     word ptr [divisor]
                pushfw
                pop     emul_flags
                ;;
                mov     emul_ax, ax     ; save emul regs
                mov     emul_dx, dx
                ;;
                jmp     emul_complete

-------------------------------------------------------------------avp/div32-

emul_div32:     cmp     dword ptr [divisor], 0  ; div by 0 ?
                jz      emul_int0
                ;;
                mov     eax, emul_eax   ; load emul regs
                mov     edx, emul_edx   ; EDX:EAX = dividend
                ;;
                cmp     edx, [divisor]  ; dividend.Hi >= divisor
                jae     try_emul_int0
                ;;
                push    emul_flags      ; division emulation
                popfw
                div     dword ptr [divisor]
                pushfw
                pop     emul_flags
                ;;
                mov     emul_eax, eax   ; save emul regs
                mov     emul_edx, edx
                ;;
                jmp     emul_complete

-------------------------------------------------------------------avp/idiv8-

emul_idiv8:     cmp     byte ptr [divisor], 0   ; div by 0 ?
                jz      emul_int0
                ;;
                mov     ax, emul_ax     ; load emul regs
                ;;                      ; AX = dividend
                cmp     ax, 80h         ; dividend >= 80h
                jae     emul_complete
                ;;
                push    emul_flags      ; division emulation
                popfw
                idiv    byte ptr [divisor]
                pushfw
                pop     emul_flags
                ;;
                mov     emul_ax, ax     ; save emul regs
                ;;
                jmp     emul_complete

------------------------------------------------------------------avp/idiv16-

emul_idiv16:    cmp     word ptr [divisor], 0   ; div by 0 ?
                jz      emul_int0
                ;;
                mov     ax, emul_ax     ; load emul regs
                mov     dx, emul_dx     ; DX:AX = dividend
                ;;
                cmp     dx, 0           ; dividend.Hi = 0
                jnz     emul_complete
                cmp     ax, 8000h       ; dividend.Lo >= 8000h
                jae     emul_complete
                ;;
                push    emul_flags      ; division emulation
                popfw
                idiv    word ptr [divisor]
                pushfw
                pop     emul_flags
                ;;
                mov     emul_ax, ax     ; save emul regs
                mov     emul_dx, dx
                ;;
                jmp     emul_complete

------------------------------------------------------------------avp/idiv32-

emul_idiv32:    cmp     dword ptr [divisor], 0  ; div by 0 ?
                jz      emul_int0
                ;;
                mov     eax, emul_eax   ; load emul regs
                mov     edx, emul_edx   ; EDX:EAX = dividend
                ;;
                cmp     edx, 0          ; dividend.Hi = 0
                jnz     emul_complete
                cmp     eax, 80000000h  ; dividend.Lo >= 2^31
                jae     emul_complete
                ;;
                push    emul_flags      ; division emulation
                popfw
                idiv    dword ptr [divisor]
                pushfw
                pop     emul_flags
                ;;
                mov     emul_eax, eax   ; save emul regs
                mov     emul_edx, edx
                ;;
                jmp     emul_complete

-----------------------------------------------------------------------------
</pre>
<p>Now, some comments.</p>
<p>From the 12 subroutines alogorithmically-correct register-check has only 5 ones. All subroutines has correct or even redundand error-checking, so there is no way to fuckup 'em.</p>
<p>Most of register combinations will be emulated incorrectly.</p>
<ol>
<li> subroutines: div8 and div16
<ul>
<li><strong>bogdanov:</strong> correct algorithm, minimal size of code</li>
<li><strong>danilov: </strong> algorithm is correct, but bogdanov has better one; code sucks. redundand overflow checking.</li>
</ul></li>
<li> subroutine: div32
<ul>
<li><strong>bogdanov:</strong> correct algorithm, minimal size of code</li>
<li><strong>danilov: </strong> incorrect algorithm, 'coz of bug; redundand erroneous zero-dividend checking.</li>
</ul></li>
<li> subroutine: idiv8
<ul>
<li><strong>bogdanov:</strong> incorrect algorithm - only 128 numbers (from 65536) will be divided. absent overflow-checking.</li>
<li><strong>danilov: </strong> incorrect algorithm, but a bit better than bogdanov's one; for example, AX=C0FF and CL=81 is IDIV-ided correctly, but emulator will begin INT 0 emulation</li>
</ul></li>
<li> subroutine: idiv16
<ul>
<li><strong>bogdanov:</strong> incorrect algorithm - only 32768 numbers (from 2^32) will be divided; absent overflow-checking.</li>
<li><strong>danilov: </strong> too many code; incorrect algorithm, but a bit better than bogdanov's one for example, DX=C000, AX=FFFF, CX=8001 are IDIV-ided ok, but drweb emulates INT 0</li>
</ul></li>
<li> subroutine: idiv32
<ul>
<li><strong>bogdanov:</strong> incorrect algorithm, only 80000000h numbers (from 2^64) will be divided; absent overflow-checking.</li>
<li><strong>danilov: </strong> completely absent</li>
</ul></li>
</ol>
<p>Competition results.</p>
<p>Nobody emulated IDIV correctly.</p>
<table border="1" cellspacing="0" cellpadding="0" summary="">
<tr><td rowspan="2">&nbsp;</td><th colspan="4">subroutines</th></tr>
<tr> <th> total </th><th> programmed </th><th> correct </th><th> incorrect </th></tr>
<tr><th> bogdanov </th><td> 6 </td><td> 6 </td><td> 3 </td><td> 3 </td></tr>
<tr><th> danilov </th><td> 6 </td><td> 5 </td><td> 2 </td><td> 3 </td></tr>
</table>
<p>But, danilov gives as a hope, that sometimes... sometimes he will write correct emulator. As it seems, his code was modifed several times, he even tried to do such things as overflow checking and zero-dividend checking. Also, register checking before IDIV are looking as stealed from somewhere, but with errors.</p>
<p>From the other side, bigdanov's emulator works only with 1% of possible number combinations, so it has no overflow checking.</p>
<p>So, DANILOV wins!</p>
<p>And here is some bonus:</p>
<pre class="source">
 ; <strong>subroutine: IDIV emulation, returns CF=1 instead of INT 0.</strong>
 ; input:  EDX:EAX = dividend
 ;         ECX = divisor
 ; output: CF = 0 -- all ok, EAX = quotient, EDX = remainder
 ;         CF = 1 -- error (div0 or overflow)

emul_idiv:              pusha
                        xor     bh, bh
                        or      ecx, ecx
                        jz      __div_err
                        jg      __g1
                        neg     ecx
                        or      bh, 1
__g1:                   or      edx, edx
                        jge     __g2
                        neg     edx
                        neg     eax
                        sbb     edx, 0
                        xor     bh, 3
__g2:                   xor     esi, esi        ; d
                        xor     edi, edi        ; m
                        mov     bl, 64
__divcycle:             shl     esi, 1          ; d &lt;&lt;= 1
                        jc      __div_err
                        shl     eax, 1          ; x &lt;&lt;= 1
                        rcl     edx, 1
                        rcl     edi, 1          ; m = (m &lt;&lt; 1) | x.bit[i]
                        jc      __cmpsub
                        cmp     edi, ecx
                        jb      __cmpsubok
__cmpsub:               sbb     edi, ecx
                        or      esi, 1
__cmpsubok:             dec     bl
                        jnz     __divcycle
                        or      esi, esi
                        js      __div_err
                        or      edi, edi
                        js      __div_err
                        test    bh, 1
                        jz      __skipneg1
                        neg     esi
__skipneg1:             test    bh, 2
                        jz      __skipneg2
                        neg     edi
__skipneg2:             mov     [esp+7*4], esi
                        mov     [esp+5*4], edi
                        popa
                        clc
                        retn
__div_err:              popa
                        stc
                        retn
</pre>
<p>That's all!</p>
[<a style="" href="/lib/?lang=EN&amp;index=AR#vzo22">Back to index</a>] [<a href="/lib/vzo22.html#disqus_thread">Comments</a>]<br/> <div id="disqus_thread"></div>
<script type="text/rocketscript">
        /* * * CONFIGURATION VARIABLES: EDIT BEFORE PASTING INTO YOUR WEBPAGE * * */
        var disqus_shortname = 'vxheaven'; // required: replace example with your forum shortname

        /* * * DON'T EDIT BELOW THIS LINE * * */
        (function() {
            var dsq = document.createElement('script'); dsq.type = 'text/javascript'; dsq.async = true;
            dsq.src = '//' + disqus_shortname + '.disqus.com/embed.js';
            (document.getElementsByTagName('head')[0] || document.getElementsByTagName('body')[0]).appendChild(dsq);
        })();
    </script>
<noscript>Please enable JavaScript to view the <a href="http://disqus.com/?ref_noscript">comments powered by Disqus.</a></noscript></div>
<div><small>By accessing, viewing, downloading or otherwise using this content you agree to be bound by the <a href="/agreement.php">Terms of Use</a>!</small> <small>vxheaven.org aka vx.netlux.org</small></div>
<div style="margin-top: 2px; float: left;" class="adsapeu">
<script type="text/rocketscript">
<!--
var _acic={dataProvider:10};(function(){var e=document.createElement("script");e.type="text/javascript";e.async=true;e.src="//www.acint.net/aci.js";var t=document.getElementsByTagName("script")[0];t.parentNode.insertBefore(e,t)})()
//-->
</script>
</div>
<script data-rocketsrc="http://www.google-analytics.com/urchin.js" type="text/rocketscript"></script><script type="text/rocketscript">try { _uacct = "UA-590608-1"; urchinTracker(); } catch(err) {}</script>
<div style="display: none;"><a href="/lib/index.php?lang=de&amp;id=vzo22">de</a><a href="/lib/index.php?lang=en&amp;id=vzo22">en</a><a href="/lib/index.php?lang=es&amp;id=vzo22">es</a><a href="/lib/index.php?lang=it&amp;id=vzo22">it</a><a href="/lib/index.php?lang=fr&amp;id=vzo22">fr</a><a href="/lib/index.php?lang=pl&amp;id=vzo22">pl</a><a href="/lib/index.php?lang=ru&amp;id=vzo22">ru</a><a href="/lib/index.php?lang=ua&amp;id=vzo22">ua</a></div>
</body>
</html>
